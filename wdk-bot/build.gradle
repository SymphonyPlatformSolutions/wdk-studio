plugins {
  id 'java-library'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
    mavenContent {
      snapshotsOnly()
    }
  }
}

configurations {
  botJar
}

dependencies {
  compileOnly 'org.finos.symphony.wdk:workflow-language:1.4.0-SNAPSHOT'
  implementation 'org.apache.commons:commons-text:1.9'
  botJar 'org.finos.symphony.wdk:workflow-bot-app:1.4.0-SNAPSHOT:boot'
}

tasks.register('botJar', Copy) {
  description "Downloads WDK jar."
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from configurations.botJar
  into project.projectDir
  rename { f -> "workflow-bot-app.jar" }
}

tasks.register('customActivityLibs', Copy) {
  description "Copies custom activity dependencies in lib folder."
  from jar
  from configurations.runtimeClasspath
  into "lib"
}

tasks.build.dependsOn tasks.botJar
tasks.build.dependsOn tasks.customActivityLibs
