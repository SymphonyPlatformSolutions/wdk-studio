plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.0.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}

configurations {
    botJar
}

dependencies {
    implementation 'org.finos.symphony.bdk:symphony-bdk-app-spring-boot-starter:2.13.0'
    compileOnly 'org.springframework:spring-tx'
    compileOnly 'org.finos.symphony.wdk:workflow-language:1.5.1'
    compileOnly 'org.finos.symphony.wdk:workflow-bot-app:1.5.1'
    botJar 'org.finos.symphony.wdk:workflow-bot-app:1.5.1:boot'
}

tasks.register('botJar', Copy) {
    description "Downloads WDK jar."
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.botJar
    into project.projectDir
    rename { f -> "workflow-bot-app.jar" }
}

tasks.register('customActivityLibs', Copy) {
    description "Copies custom activity dependencies in lib folder."
    from jar
    from configurations.runtimeClasspath
    into "lib"
}

tasks.register('cleanLib', Delete) {
    delete 'lib'
}

tasks.register('removeExtraLibs', Delete) {
    delete fileTree('lib').matching {
        exclude 'wdk-studio.jar', 'symphony-bdk-app*'
    }
}

tasks.customActivityLibs.dependsOn tasks.cleanLib
tasks.removeExtraLibs.dependsOn tasks.customActivityLibs
tasks.build.dependsOn tasks.removeExtraLibs
tasks.botJar.dependsOn tasks.build
